plugins {
    id "java"
    id "java-gradle-plugin"
    id "maven-publish"
}

group = "io.github.kale_ko"
version = project_version

repositories {
    maven {
        name = "Kales-Maven"
        url = "https://maven.kaleko.dev/public-snapshot/"

        credentials {
            username = System.getenv("MAVEN_USER")
            password = System.getenv("MAVEN_TOKEN")
        }
    }
    mavenCentral()
}

dependencies {
    gradleApi()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(java_version)
    }

    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

gradlePlugin {
    website = "https://github.com/Kale-Ko/GradleShade/"
    vcsUrl = "https://github.com/Kale-Ko/GradleShade.git"

    plugins {
        gradleshade {
            id = "io.github.kale_ko.gradleshade"
            displayName = "GradleShade"

            description = "A Gradle extension that automatically shades/shadows dependencies into a jar for standalone execution using a few different methods."
            tags.addAll("jar", "java", "kotlin", "shade", "shaded", "shaded-jar", "shadow", "shadowed", "shadowed-jar", "fat", "fat-jar")

            implementationClass = "io.github.kale_ko.gradleshade.GradleShadePlugin"
        }
    }
}

publishing {
    repositories {
        maven {
            name = "Kales-Maven-Snapshot"
            url = "https://maven.kaleko.dev/public-snapshot/"

            credentials {
                username = System.getenv("MAVEN_USER")
                password = System.getenv("MAVEN_TOKEN")
            }
        }

        if ("release".equalsIgnoreCase(System.getenv("MAVEN_TYPE"))) {
            maven {
                name = "Kales-Maven-Release"
                url = "https://maven.kaleko.dev/public-release/"

                credentials {
                    username = System.getenv("MAVEN_USER")
                    password = System.getenv("MAVEN_TOKEN")
                }
            }
        }
    }
}